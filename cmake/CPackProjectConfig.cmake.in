# We probably need to reconfigure the liberis.pc file: the DEB and RPM targets
# install to /usr rather than ${CMAKE_INSTALL_PREFIX}
if(NOT CMAKE_INSTALL_PREFIX STREQUAL /usr)
    if(CPACK_GENERATOR STREQUAL DEB OR CPACK_GENERATOR STREQUAL RPM)
        set(CPACK_INSTALL_SCRIPT ${CPACK_PACKAGE_DIRECTORY}/RegenPkgConfig.cmake)
    endif()
endif()

set(eris_package_version "@ERIS_VERSION@")
if(NOT "@CMAKE_BUILD_TYPE@" STREQUAL "Release")
    execute_process(COMMAND git rev-parse --short=10 "@"
        RESULT_VARIABLE eris_git_rev_result
        OUTPUT_VARIABLE eris_git_rev
        OUTPUT_STRIP_TRAILING_WHITESPACE
        )
    if(NOT eris_git_rev_result EQUAL 0)
        message(FATAL_ERROR "Unable to determine git revision tag for non-Release build")
    endif()
    execute_process(COMMAND git show -s "--format=%ci" "@"
        RESULT_VARIABLE eris_build_date_result
        OUTPUT_VARIABLE eris_build_date
        OUTPUT_STRIP_TRAILING_WHITESPACE
        )
    if(NOT eris_build_date_result EQUAL 0)
        message(FATAL_ERROR "Unable to determine commit date for non-Release build")
    endif()
    string(REGEX REPLACE "^([0-9][0-9][0-9][0-9])-([0-9][0-9])-([0-9][0-9]).*" "\\1\\2\\3" eris_build_datestr "${eris_build_date}")
    execute_process(COMMAND git tag -l "v@ERIS_VERSION@"
        RESULT_VARIABLE eris_git_tagged_result
        OUTPUT_VARIABLE eris_git_tagged
        OUTPUT_STRIP_TRAILING_WHITESPACE
        )
    if(NOT eris_git_rev_result EQUAL 0)
        message(FATAL_ERROR "Unable to run git tag to determine pre/post-release build status")
    endif()
    set(gitversep "~")
    if (eris_git_tagged)
        set(gitversep "+")
    endif()

    set(eris_package_version "${eris_package_version}${gitversep}git${eris_build_datestr}.${eris_git_rev}")
endif()
set(CPACK_PACKAGE_FILE_NAME "eris_${eris_package_version}_@arch@")
set(CPACK_DEBIAN_PACKAGE_VERSION "${eris_package_version}")
